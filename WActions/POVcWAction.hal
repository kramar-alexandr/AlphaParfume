external inner function val CalcPalletsNr(val,string);
external inner function Boolean PODchrsum(record POVc,Integer);
external inner function string 255 FindINObjects(string,string);
external inner procedure GetINPurchaseControlAcc(record INVc,var string,string,string,Integer);
external inner function Boolean PUVc_PasteArtCode(var record PUVc,Integer,var string,var string);
external inner function Boolean GetFirstItem(var string,var record INVc);
external inner function val DivRateToBase1(string,val,val,val,val,val,val,roundmode);
external inner function val MulWithRateToBase1(var string,Date,val,roundmode);
external inner function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external inner procedure POMultiLineText(record POVc,record INVc,string,Integer);
external inner function Boolean GetItemNameStr(Integer,var string,string,string,string);
external inner function Boolean FindItemVAR2(string,var string,var string,var string);
external inner procedure GetItemVATCode(string,Integer,var string,Boolean);
external inner procedure POSumup(var record POVc );
external inner function Boolean PODClassSwitchRow(Integer,Integer);
external inner function Boolean POVc_PasteArtCode(var record POVc,Integer,Boolean);


function Boolean tPOVc_PasteArtCode(var record POVc POp,Integer rownr,Boolean multilinef)
BEGIN
Boolean res,itemfound,found,varf;
row POVc POrw;
record POSettingBlock POset;
record INVc INr;
record INVc IN2r;
record tINKVc INKr;
//row PIVc INKrw;
string 255 itsz,mask,subsets,vatcode;
Integer rwcnt,i;
val t,price;
string 255 tstr;
string 255 mskrep;

MatRowGet(POp,rownr,POrw);
itemfound = GetFirstItem(POrw.ArtCode,INr);
if (itemfound==false) then
	begin
	itemfound = ReadFirstItem(POrw.ArtCode,INr,true,false);
	end
else
	begin
	POrw.ArtCode = INr.Code;
	end;
if (itemfound) then
	begin
	POrw.PriceFactor = INr.PriceFactor;
	end
else
	begin
	POrw.PriceFactor = blankval;
	end;
//INKr.VECode = POp.VECode;
INKr.Code = POrw.ArtCode;
found = ReadFirstMain(INKr,1,true);
if (found==false) then
	begin
	//INKr.VECode = POp.VECode;
	INKr.Code = INr.Code;
	found = ReadFirstMain(INKr,1,true);
	if (FindItemVAR2(POrw.ArtCode,itsz,mask,subsets)) then
		begin
		if (ReadFirstItem(POrw.ArtCode,IN2r,true,true)) then
			begin
			INKr.Name = IN2r.Name;
			end;
		end;
	end;
if (found) then
	begin
	price = INKr.Price;
	if (INKr.Curncy!=POp.CurncyCode) then
		begin
		t = MulWithRateToBase1(INKr.Curncy,POp.TransDate,INKr.Price,DefaultCurRoundOff);
		price = DivRateToBase1(POp.CurncyCode,t,POp.FrRate,POp.ToRateB1,POp.ToRateB2,POp.BaseRate1,POp.BaseRate2,DefaultCurRoundOff);
		end;
	vatcode = POp.VEVATCode;
	if (blank(vatcode)) then
		begin
		GetItemVATCode(INr.Code,POp.ExportFlag,vatcode,false);
		end;
	POrw.Price = price;
	POrw.VEArtCode = "";
	//POrw.CustomsCost = INKr.PurchaseCost;
	POrw.Spec = INKr.Name;
	POrw.VATCode = vatcode;
	//POrw.UnitCode = INKr.Unittext;
	POrw.VEUnit =  INKr.Unittext;
	if (itemfound) then
		begin
		if (blank(POrw.Objects)) then
			begin
			POrw.Objects = FindINObjects(INr.Objects,INr.Group);
			end;
		end;
	POrw.PIFactor = blankval;
	//DivPIFactor(POrw.Quant,POrw.PIFactor,t);
	//POrw.VEQuant = t;
	POrw.VEQuant = POrw.Quant;
	POrw.VEQuant = POrw.VEQuant;
	MatRowPut(POp,rownr,POrw);
	MatRowGet(POp,rownr,POrw);
	res = true;
	end
else
	begin
	if (itemfound) then
		begin
		price = DivRateToBase1(POp.CurncyCode,INr.InPrice,POp.FrRate,POp.ToRateB1,POp.ToRateB2,POp.BaseRate1,POp.BaseRate2,DefaultCurRoundOff);
		vatcode = POp.VEVATCode;
		if (blank(vatcode)) then
			begin
			GetItemVATCode(INr.Code,POp.ExportFlag,vatcode,false);
			end;
		if (GetItemNameStr(1,mask,POp.LangCode,INr.Name,INr.Code)) then
			begin
			end;
//     			 POrw.ArtCode = INr.Code; // Why? What if POrw has variety item?
		POrw.Price = price;
		POrw.VEArtCode = "";
		POrw.Spec = mask;
		if (multilinef==false) then
			begin
			POMultiLineText(POp,INr,POp.LangCode,rownr);
			end;
		POrw.VATCode = vatcode;
		POrw.PIFactor = blankval;
		POrw.VEQuant = POrw.Quant;
		POrw.VEQuant = POrw.VEQuant;
		INr.Code = POrw.ArtCode;
		res = true;
		end
	else
		begin
		res = false;
		end;
	end;
POrw.Objects = FindINObjects(INr.Objects,INr.Group);
POrw.UnitCode = INr.Unittext;
GetINPurchaseControlAcc(INr,tstr,POp.VECode,POp.Location,POrw.StockType);
POrw.CostAcc = tstr;
POrw.PalletNr = CalcPalletsNr(POrw.Quant,POrw.ArtCode);
MatRowPut(POp,rownr,POrw);
if (PODchrsum(POp,rownr)) then
	begin
	POSumup(POp);
	end;
tPOVc_PasteArtCode = res;

RETURN;
END;


function Boolean ttPOVc_PasteArtCode(var record POVc POp,Integer rownr,Boolean multilinef)
BEGIN
Boolean res;
row POVc POrw;

MatRowGet(POp,rownr,POrw);
if left(POrw.ArtCode,2) == "KA" then
	begin
	res = tPOVc_PasteArtCode(POp, rownr, multilinef);
	end
else
	begin
	res = POVc_PasteArtCode(POp, rownr, multilinef);
	end;
ttPOVc_PasteArtCode = res;
RETURN;
END;


function Boolean PODClassArtCodeEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record POVc POr;
  row POVc POrw;

  GetWindowRecord(wn,POr);
  MatRowGet(POr,rownr,POrw);
  if ((changedf) or (blank(POrw.Spec) and (nonblank(POrw.ArtCode)))) then begin
    if (ttPOVc_PasteArtCode(POr,rownr,false)) then begin
      PutWindowRecord(wn,POr);
      if (PODClassSwitchRow(wn,rownr)) then begin end;
    end else begin
      Beep;
    end;
  end;
  PODClassArtCodeEFAfter = true;
  return;
end;


global
function Boolean PODClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
begin
  Boolean res;

  switch (fieldname)
    begin
      case "ArtCode": res = PODClassArtCodeEFAfter(wn,rownr,changed!=0);
      otherwise res = inner.PODClassAfterEditField(wn, fieldname, fn, rownr, changed);
    end;
  PODClassAfterEditField = res;
  Return;
end;
